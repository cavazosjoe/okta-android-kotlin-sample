plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def oktaProperties = new Properties()
rootProject.file("okta.properties").withInputStream { oktaProperties.load(it) }

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.okta.android.samples.browser_sign_in"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        buildConfigField "String", 'DISCOVERY_URL', "\"${oktaProperties.getProperty('discoveryUrl')}\""
        buildConfigField "String", 'CLIENT_ID', "\"${oktaProperties.getProperty('clientId')}\""
        buildConfigField "String", 'SIGN_IN_REDIRECT_URI', "\"${oktaProperties.getProperty('signInRedirectUri')}\""
        buildConfigField "String", 'SIGN_OUT_REDIRECT_URI', "\"${oktaProperties.getProperty('signOutRedirectUri')}\""
    }

    defaultConfig.manifestPlaceholders = [
            "webAuthenticationRedirectScheme": parseScheme(oktaProperties.getProperty('signInRedirectUri'))
    ]

    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    // Android studio doesn't support newer versions?
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.3'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.25'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.activity:activity-ktx:1.9.3'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation(platform('com.okta.kotlin:bom:1.0.0'))
    implementation('com.okta.kotlin:auth-foundation-bootstrap')
    implementation('com.okta.kotlin:web-authentication-ui')
}

static def parseScheme(String uri) {
    def index = uri.indexOf(':/')
    if (index == -1) {
        throw new IllegalStateException("Scheme is not in a valid format.")
    }
    return uri.substring(0, index)
}
